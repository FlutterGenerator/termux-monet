plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace = "com.termux.shared"
    compileSdkVersion project.properties.compileSdkVersion.toInteger()

    defaultConfig {
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            ndkBuild {
                cppFlags ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled = true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    externalNativeBuild {
        ndkBuild {
            path file('src/main/cpp/Android.mk')
        }
    }
    ndkVersion = System.getenv("JITPACK_NDK_VERSION") ?: project.properties.ndkVersion
    buildFeatures {
        viewBinding = false
    }
}

dependencies {
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
    coreLibraryDesugaring libs.desugar.jdk.libs

    implementation libs.appcompat
    implementation libs.annotation

    implementation libs.drawerlayout

    implementation libs.core
    implementation libs.material
    implementation libs.guava
    implementation libs.markwon.core
    implementation libs.ext.strikethrough
    implementation libs.linkify
    implementation libs.recycler
    //noinspection Aligned16KB
    implementation libs.restrictionbypass

    implementation libs.window

    implementation libs.viewpager
    implementation libs.viewpager2
    implementation libs.annotation.experimental
    implementation libs.constraintlayout
    implementation libs.fragment.ktx
    implementation libs.lifecycle.livedata.core.ktx
    implementation libs.loader
    implementation libs.recyclerview
    implementation libs.savedstate
    implementation libs.transition
    implementation libs.versionedparcelable
    implementation libs.coordinatorlayout

    implementation libs.vectordrawable.animated

    // Bumping commons-io to a version higher than 2.5
    // will cause runtime exceptions on android < 8 due
    // to missing classes like java.nio.file.Path.
    implementation libs.commons.io

    implementation project(":terminal-view")

    implementation project(":termux-am-library")
}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation"
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.findByName('release')
                groupId = 'com.termux'
                artifactId = 'termux-shared'
                version = '0.118.0'
                artifact(sourceJar)
            }
        }
    }
}
