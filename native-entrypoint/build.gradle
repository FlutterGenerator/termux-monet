apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace = "com.termux.nativeentrypoint"

    compileSdk = project.properties.compileSdk.toInteger()
    ndkVersion = System.getenv("JITPACK_NDK_VERSION") ?: project.properties.ndkVersion

    defaultConfig {
        minSdk = project.properties.minSdk.toInteger()
        targetSdk = project.properties.targetSdk.toInteger()

        externalNativeBuild {
            ndkBuild {
                cFlags "-Wall", "-Wextra", "-Werror", "-Os", "-fno-stack-protector", "-Wl,--gc-sections"
            }
        }

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
        }
        debug {
            debuggable true
            jniDebuggable false
            minifyEnabled false
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation"
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.findByName('release')
                groupId = 'com.termux'
                artifactId = 'native-entrypoint'
                version = '1.0'
                artifact(sourceJar)
            }
        }
    }
}
